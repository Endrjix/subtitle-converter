buildscript {
	ext {
		springBootVersion = '1.3.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

jar {
	baseName = 'subtitle-converter'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

checkstyle {
	toolVersion = "6.7"
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter')
	compile('org.springframework:spring-context-support')
	compile('commons-io:commons-io:2.4')
	compile('dom4j:dom4j:1.6.1')
	compile('jaxen:jaxen:1.1.6')
	compile('joda-time:joda-time:2.0')
	compile('org.apache.velocity:velocity')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}


eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.9'
}

jacocoTestReport {
	group = "Reporting"
	reports {
		xml.enabled true
		csv.enabled false
		html.destination "${buildDir}/reports/coverage"
	}
}

findbugs {
	effort = "max"
	reportLevel = "medium"
}

tasks.withType(FindBugs) {
	reports {
		xml.enabled = true
	}
}
